import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

def CLOUD_PUBSUB_VERSION = '1.37.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.cloud:google-cloud-pubsub:${CLOUD_PUBSUB_VERSION}"

    implementation "com.fasterxml.jackson.core:jackson-core:2.1.3"
    implementation "com.google.api:api-common:1.6.0"
    implementation "com.google.api:gax:1.29.0"
    implementation "com.google.api:gax-grpc:1.29.0"
    implementation "com.google.api.grpc:grpc-google-cloud-pubsub-v1:1.19.1"
    implementation "com.google.api.grpc:proto-google-cloud-pubsub-v1:1.19.1"
    implementation "com.google.api.grpc:proto-google-common-protos:1.12.0"
    implementation "com.google.api.grpc:proto-google-iam-v1:0.12.0"
    implementation "com.google.auth:google-auth-library-credentials:0.9.1"
    implementation "com.google.auth:google-auth-library-oauth2-http:0.9.1"
    implementation "com.google.auto.value:auto-value:1.4"
    implementation "com.google.cloud:google-cloud-core:1.37.1"
    implementation "com.google.cloud:google-cloud-core-grpc:1.37.1"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "com.google.code.gson:gson:2.7"
    implementation "com.google.errorprone:error_prone_annotations:2.2.0"
    implementation "com.google.guava:guava:20.0"
    implementation "com.google.http-client:google-http-client:1.23.0"
    implementation "com.google.http-client:google-http-client-jackson2:1.19.0"
    implementation "com.google.protobuf:protobuf-java:3.6.0"
    implementation "com.google.protobuf:protobuf-java-util:3.6.0"
    implementation "com.google.protobuf:protobuf-lite:3.0.1"
    implementation "commons-codec:commons-codec:1.3"
    implementation "commons-logging:commons-logging:1.1.1"
    implementation "io.grpc:grpc-auth:1.13.1"
    implementation "io.grpc:grpc-context:1.13.1"
    implementation "io.grpc:grpc-core:1.13.1"
    implementation "io.grpc:grpc-netty-shaded:1.13.1"
    implementation "io.grpc:grpc-protobuf:1.13.1"
    implementation "io.grpc:grpc-protobuf-lite:1.13.1"
    implementation "io.grpc:grpc-stub:1.13.1"
    implementation "io.opencensus:opencensus-api:0.12.3"
    implementation "io.opencensus:opencensus-contrib-grpc-metrics:0.12.3"
    implementation "joda-time:joda-time:2.9.2"
    implementation "org.apache.httpcomponents:httpclient:4.0.1"
    implementation "org.apache.httpcomponents:httpcore:4.0.1"
    implementation "org.threeten:threetenbp:1.3.3"
}

task generateGemJarRequiresFile {
    doLast {
        File jars_file = file('lib/logstash-input-google_pubsub_jars.rb')
        jars_file.newWriter().withWriter { w ->
            w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
            w << "require \'jar_dependencies\'\n"
            configurations.runtimeClasspath.allDependencies.each {
                w << "require_jar(\'${it.group}\', \'${it.name}\', \'${it.version}\')\n"
            }
        }
    }
}

task vendor {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        configurations.runtimeClasspath.allDependencies.each { dep ->
            File f = configurations.runtimeClasspath.filter { it.absolutePath.contains("${dep.group}/${dep.name}/${dep.version}") }.singleFile
            String groupPath = dep.group.replaceAll('\\.', '/')
            File newJarFile = file("${vendorPathPrefix}/${groupPath}/${dep.name}/${dep.version}/${dep.name}-${dep.version}.jar")
            newJarFile.mkdirs()
            Files.copy(f.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
        }
    }
}

vendor.dependsOn(generateGemJarRequiresFile)
